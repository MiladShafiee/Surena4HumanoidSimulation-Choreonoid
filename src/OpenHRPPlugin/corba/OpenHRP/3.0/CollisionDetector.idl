
#ifndef OPENHRP_COLLISION_DETECTOR_IDL_INCLUDED
#define OPENHRP_COLLISION_DETECTOR_IDL_INCLUDED

/** @file CollisionDetector/corba/CollisionDetector.idl
 * @if jp
 * CollisionDetector サーバ関連インターフェース
 * @endif
 */

#include <cnoid/corba/OpenHRP/3.0/OpenHRPCommon.idl>
#include <cnoid/corba/OpenHRP/3.0/ModelLoader.idl>

module OpenHRP {

  /**
   * @if jp
   * CollisionDetector インターフェース
   *
   * このサーバは衝突検出とその法線ベクトルの算出を行います。
   * 通常、 DynamicsSimulator から呼び出されるため、直接このサーバにアクセスする必要はありません。
   * @endif
   */
  interface CollisionDetector
  {
    void destroy();
  
    /**
     * @if jp
     * @brief キャラクタを追加します。
     * @param   charName    キャラクタ名
     * @param   model	    モデル情報
     * @else
     * Adding Objects
     * @param   charName    Character Name
     * @param   model	    model information
     * @endif
    */
    void
    addModel
    (
     in string charName,
     in CharacterInfo model
     );
  
    /**
     * @if jp
     * @brief 干渉チェックペアを追加します。
     * @param   colPair       干渉チェックペア
     * @param   convexsize1   ペアのうち、1つ目を凸包集合に変換するか
     * @param   convexsize2   ペアのうち、2つ目を凸包集合に変換するか
     * @else
     * Adding Polygon Set Pairs
     * @param   colPair       Collision Pair
     * @param   convexsize1   Flag to set whether object1 
     *  			      should be converted to a set of Convexes
     * @param   convexsize2   Flag to set whether object2
     * 			      should be converted to a set of Convexes
     * @endif
    */
    void
    addCollisionPair
    (
     in LinkPair   colPair,
     in boolean convexsize1,
     in boolean convexsize2
     );
  
    /**
     * @if jp
     * @brief ペアを取り除きます。
     * @param colPair 取り除くペア
     * @else
     * Delete a pair of Polygon Sets
     * @param   colPair       Collision Pair
     * @endif
     */
    void
    removeCollisionPair
    (
     in LinkPair   colPair
     );
  
    /**
     * @if jp
     * 既に設定されたペアから衝突検出を行います。
     * @param checkAll 全てをチェックするか(falseを設定するとひとつ衝突を検出した時点で終了します)
     * @param positions キャラクタの位置/姿勢
     * @param collidedPairs 衝突しているペアの配列(衝突していなければ長さは0)
     * @return ひとつでも衝突していれば true, 衝突していなければ false
     * @else
     * Check for Collision between pre-defined Pairs
     * @param  checkAll     true:  Check All Pairs
     * 		  false: Stop when 1 collision is found
     * @param  characters    Position and Attitude of Object
     * @param  collidedPairs  Array of Pairs that are colliding(If none length is 0)
     * @return true:  At least one pair is colliding
     *      false: No pairs are colliding
     * @endif
     */
    boolean queryIntersectionForDefinedPairs
    (
     in boolean checkAll,
     in CharacterPositionSequence positions,
     out LinkPairSequence collidedPairs
     );
  
    /**
     * @if jp
     * ペアを設定し衝突検出を行います。
     * @param checkAll 全てのペアをチェックするか(falseを設定するとひとつ衝突を検出した時点で終了します)
     * @param checkPairs ペア
     * @param positions キャラクタの位置/姿勢
     * @param collidedPairs 衝突しているペアの配列(衝突していなければ長さは0)
     * @return ひとつでも衝突していれば true, 衝突していなければ false
     * @else
     * Check for Collision between Given Pairs
     *    
     * @param  checkAll     true:  Check All Pairs
     *                     false: Return when 1 collision is found
     * @param  checkPairs   Array of Pairs to check for Collision
     * @param  characters    Position and Attitude of Object
     * @param  collidedPairs  Array of Pairs that are colliding(If none length is 0)
     * @return true:  At least one pair is colliding
     *         false: No pairs are colliding
     * @endif
     */
    boolean queryIntersectionForGivenPairs
    (
     in boolean checkAll,
     in LinkPairSequence checkPairs,
     in CharacterPositionSequence positions,
     out LinkPairSequence collidedPairs
     );
  
    /**
     * @if jp
     * すでに設定したペアの衝突情報を取得します。
     * @param characters キャラクタの位置/姿勢
     * @param colInfos 衝突情報
     * @return ひとつでも衝突していれば true, 衝突していなければ false
     * @else
     * Get Collision State Information of pre-defined Pairs
     *
     * @param  characters    Position of Object
     * @param  colInfos     Collision Information
     * @return true:        At least one pair is colliding
     *         false:       No pairs are colliding
     * @endif
     */
    boolean queryContactDeterminationForDefinedPairs(
						     in CharacterPositionSequence positions,
						     out CollisionSequence collisions
						     );

    /**
     * @if jp
     * ペアを与え衝突情報を取得します。
     * @param checkPairs ペア
     * @param characters キャラクタの位置/姿勢
     * @param colInfos 衝突情報
     * @return ひとつでも衝突していれば true, 衝突していなければ false
     * @else
     * Get Collision State Information for given Pairs
     *
     * @param  checkPairs   Array of pairs to check for collision
     * @param  characters    Position of Object
     * @param  colInfos     Collision Information
     * @return true:        At least one pair is colliding
     *         false:       No pairs are colliding
     * @endif
     */
    boolean queryContactDeterminationForGivenPairs(
						   in LinkPairSequence checkPairs,
						   in CharacterPositionSequence positions,
						   out CollisionSequence collisions
						   );
    
    /**
     * @if jp
     * キャッシュをクリアします。
     * @param url モデルファイルURL
     * @else
     * Clear the Cache
     * @param   url         Data Identifier
     * @endif
     */
    void clearCache(in string url);
    
  };

  /**
   * CollisionDetectorFactory
   */
  interface CollisionDetectorFactory : ServerObject
  {
    /**
     * @if jp
     * CollisionDetector を取得します。
     * @endif
     */
    CollisionDetector create();
  };

};

#endif
